entry = { SOI ~ line* ~ (item ~ NEWLINE+)* ~ item? ~ EOI }

item = { option | plugin | commodity | open | close | include | note | balance | document | price | event | custom | transaction | comment | budget | budget_add | budget_transfer | budget_close }

option      = { "option" ~ space+ ~ string ~ space+ ~ string }
plugin      = { "plugin" ~ space+ ~ string ~ (space+ ~ string)* }
include     = { "include" ~ space+ ~ quote_string }
commodity   = { date ~ space+ ~ "commodity" ~ space+ ~ commodity_name ~ commodity_meta? }
open        = { date ~ space+ ~ "open" ~ space+ ~ account_name ~ (space+ ~ commodity_name ~ (space* ~ "," ~ space* ~ commodity_name)*)? ~ commodity_meta? }
close       = { date ~ space+ ~ "close" ~ space+ ~ account_name }
note        = { date ~ space+ ~ "note" ~ space+ ~ account_name ~ space+ ~ string }
balance     = { date ~ space+ ~ "balance" ~ space+ ~ account_name ~ space+ ~ number ~ space+ ~ commodity_name ~ (space+ ~ "with" ~ space+ ~ "pad" ~ space+ ~ account_name)? }
document    = { date ~ space+ ~ "document" ~ space+ ~ account_name ~ space+ ~ string }
price       = { date ~ space+ ~ "price" ~ space+ ~ commodity_name ~ space+ ~ number ~ space+ ~ commodity_name }
event       = { date ~ space+ ~ "event" ~ space+ ~ string ~ space+ ~ string }
custom      = { date ~ space+ ~ "custom" ~ space+ ~ string ~ (space+ ~ string_or_account)+ ~ commodity_meta? }
transaction = { date ~ transaction_flag? ~ (space+ ~ quote_string){0, 2} ~ tags? ~ links? ~ transaction_detail }

budget          = { date ~ space+ ~ "budget" ~ space+ ~ unquote_string ~ space+ ~ commodity_name ~ commodity_meta? }
budget_add      = { date ~ space+ ~ "budget-add" ~ space+ ~ unquote_string ~ space+ ~ posting_amount ~ commodity_meta? }
budget_transfer = { date ~ space+ ~ "budget-transfer" ~ space+ ~ unquote_string ~ space+ ~ unquote_string ~ space+ ~ posting_amount ~ commodity_meta? }
budget_close    = { date ~ space+ ~ "budget-close" ~ space+ ~ unquote_string ~ commodity_meta? }

comment = { (";" | "*" | "#" | "//") ~ (!line ~ ANY)* }

COMMENT = _{ (";" | "//") ~ (!line ~ ANY)* }

transaction_flag      =  { space+ ~ ("!" | "*") }
tags                  =  { (space* ~ tag)* }
tag                   =  { "#" ~ unquote_string }
links                 =  { (space* ~ link)* }
link                  =  { "^" ~ unquote_string }
transaction_detail    = _{ identation_push ~ transaction_lines ~ DROP }
transaction_lines     =  { transaction_line ~ (transaction_next_line)* }
transaction_line      =  { transaction_posting | commodity_line }
transaction_posting   =  { transaction_flag? ~ account_name ~ (space+ ~ posting_unit)? }
transaction_next_line = _{ identation ~ transaction_line }

posting_unit   = { (posting_amount)? ~ posting_meta }
posting_amount = { number ~ space+ ~ commodity_name }
posting_meta   = { (space+ ~ "{" ~ space* ~ posting_cost ~ price_cost_date? ~ space* ~ "}")? ~ space* ~ posting_price? }

posting_cost    =  { number ~ space+ ~ commodity_name }
price_cost_date = _{ space* ~ "," ~ space* ~ date }
posting_price   =  { posting_single_price | posting_total_price }

posting_single_price = { "@" ~ space+ ~ number ~ space+ ~ commodity_name }
posting_total_price  = { "@@" ~ space+ ~ number ~ space+ ~ commodity_name }

string_or_account = { account_name | string }

commodity_meta = { identation_push ~ commodity_lines ~ DROP }

identation          = _{ line ~ PEEK_ALL }
identation_push     = _{ identation ~ PUSH(space+) }
commodity_lines     = _{ commodity_line ~ (commodity_next_line)* }
commodity_next_line = _{ identation ~ commodity_line }
commodity_line      =  { string ~ space* ~ ":" ~ space* ~ string ~ space* }

date = { datetime | date_hour | date_only }

date_only = { ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{1, 2} ~ "-" ~ ASCII_DIGIT{1, 2} }
datetime  = { ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{1, 2} ~ "-" ~ ASCII_DIGIT{1, 2} ~ " " ~ ASCII_DIGIT{1, 2} ~ ":" ~ ASCII_DIGIT{1, 2} ~ ":" ~ ASCII_DIGIT{1, 2} }
date_hour = { ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{1, 2} ~ "-" ~ ASCII_DIGIT{1, 2} ~ " " ~ ASCII_DIGIT{1, 2} ~ ":" ~ ASCII_DIGIT{1, 2} }

account_name   =  { account_type ~ (":" ~ unquote_string)+ }
account_type   =  { "Assets" | "Liabilities" | "Equity" | "Income" | "Expenses" }
string         =  { unquote_string | quote_string }
unquote_string =  { ((!("\"" | ":" | "(" | ")" | "," | " " | "\t" | line) ~ ANY) | ASCII_ALPHANUMERIC | "." | "_" | "-")+ }
quote_string   = @{ "\"" ~ inner ~ "\"" }
commodity_name =  { ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "." | "_" | "-" | "'")* }

inner = @{ char* }
char  =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
  | "\\" ~ ("u" ~ "{" ~ ASCII_HEX_DIGIT{4} ~ "}")
}

space = _{ " " | "\t" }
line  = _{ NEWLINE }

number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
