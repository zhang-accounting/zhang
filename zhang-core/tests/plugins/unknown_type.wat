;; A plugin from the future: it declares a plugin type this host does not know next to
;; `Processor`. The host must still load it, ignore the unknown type and run the processor, which
;; drops the whole stream so a test can tell that it ran. See echo.wat for the kernel ABI.
(module
  (import "extism:host/env" "alloc" (func $alloc (param i64) (result i64)))
  (import "extism:host/env" "store_u8" (func $store_u8 (param i64 i32)))
  (import "extism:host/env" "output_set" (func $output_set (param i64 i64)))

  (memory 1)
  (data (i32.const 0) "\"unknown-type\"")                  ;; 14 bytes
  (data (i32.const 16) "\"0.1.0\"")                        ;; 7 bytes
  (data (i32.const 32) "[\"Teleporter\",\"Processor\"]")   ;; 26 bytes
  (data (i32.const 64) "[]")                               ;; 2 bytes

  ;; copy `len` bytes at `ptr` into a new kernel block and make it the output
  (func $output_bytes (param $ptr i32) (param $len i32)
    (local $block i64)
    (local $i i32)
    (local.set $block (call $alloc (i64.extend_i32_u (local.get $len))))
    (block $done
      (loop $copy
        (br_if $done (i32.ge_u (local.get $i) (local.get $len)))
        (call $store_u8
          (i64.add (local.get $block) (i64.extend_i32_u (local.get $i)))
          (i32.load8_u (i32.add (local.get $ptr) (local.get $i))))
        (local.set $i (i32.add (local.get $i) (i32.const 1)))
        (br $copy)))
    (call $output_set (local.get $block) (i64.extend_i32_u (local.get $len))))

  (func (export "name") (result i32)
    (call $output_bytes (i32.const 0) (i32.const 14))
    (i32.const 0))
  (func (export "version") (result i32)
    (call $output_bytes (i32.const 16) (i32.const 7))
    (i32.const 0))
  (func (export "supported_type") (result i32)
    (call $output_bytes (i32.const 32) (i32.const 26))
    (i32.const 0))

  (func (export "processor") (result i32)
    (call $output_bytes (i32.const 64) (i32.const 2))
    (i32.const 0)))
