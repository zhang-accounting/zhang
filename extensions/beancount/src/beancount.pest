entry = { SOI ~ line* ~ (item ~ NEWLINE+)* ~ item? ~ EOI }

item = { single_line_item | metable_item | transaction | empty_space_line }

empty_space_line = { space+ }

single_line_item = _{ (option | include | push_tag | pop_tag | valuable_comment) ~ space* ~ comment? }
option           =  { "option" ~ space+ ~ string ~ space+ ~ string }
plugin           =  { "plugin" ~ space+ ~ string ~ (space+ ~ string)* }
include          =  { "include" ~ space+ ~ quote_string }
push_tag         =  { "pushtag" ~ space+ ~ tag }
pop_tag          =  { "poptag" ~ space+ ~ tag }

metable_item    = _{ metable_head ~ metas? }
metable_head    =  { (open | close | note | plugin | balance | pad | document | price | event | commodity | budget | budget_add | budget_transfer | budget_close | custom) ~ space* ~ comment? }
commodity       =  { date ~ space+ ~ "commodity" ~ space+ ~ commodity_name ~ metas? }
open            =  { date ~ space+ ~ "open" ~ space+ ~ account_name ~ (space+ ~ commodity_name ~ (space* ~ "," ~ space* ~ commodity_name)*)? ~ (space+ ~ booking_method)? }
close           =  { date ~ space+ ~ "close" ~ space+ ~ account_name }
note            =  { date ~ space+ ~ "note" ~ space+ ~ account_name ~ space+ ~ string }
pad             =  { date ~ space+ ~ "pad" ~ space+ ~ account_name ~ space+ ~ account_name }
balance         =  { date ~ space+ ~ "balance" ~ space+ ~ account_name ~ space+ ~ number_expr ~ space+ ~ commodity_name }
document        =  { date ~ space+ ~ "document" ~ space+ ~ account_name ~ space+ ~ string }
price           =  { date ~ space+ ~ "price" ~ space+ ~ commodity_name ~ space+ ~ number_expr ~ space+ ~ commodity_name }
event           =  { date ~ space+ ~ "event" ~ space+ ~ string ~ space+ ~ string }
custom          =  { date ~ space+ ~ "custom" ~ space+ ~ string ~ (space+ ~ string_or_account)+ }
budget          =  { date ~ space+ ~ "custom" ~ space+ ~ "budget" ~ space+ ~ unquote_string ~ space+ ~ commodity_name ~ metas? }
budget_add      =  { date ~ space+ ~ "custom" ~ space+ ~ "budget-add" ~ space+ ~ unquote_string ~ space+ ~ posting_amount ~ metas? }
budget_transfer =  { date ~ space+ ~ "custom" ~ space+ ~ "budget-transfer" ~ space+ ~ unquote_string ~ space+ ~ unquote_string ~ space+ ~ posting_amount ~ metas? }
budget_close    =  { date ~ space+ ~ "custom" ~ space+ ~ "budget-close" ~ space+ ~ unquote_string ~ metas? }

comment          = _{ (";" | "*" | "#" | "//") ~ (!line ~ ANY)* }
valuable_comment =  { space* ~ comment_prefix ~ space* ~ comment_value }
comment_prefix   =  { ";" | "*" | "#" | "//" }
comment_value    =  { (!line ~ ANY)* }

transaction = { date ~ transaction_flag? ~ (space+ ~ quote_string){0, 2} ~ tags_or_links? ~ space* ~ comment? ~ transaction_lines }

transaction_flag      =  { space+ ~ ("!" | "*" | "#" | ASCII_ALPHA_UPPER) }

tags_or_links = { (space* ~ (tag | link))* }
tag                   =  { "#" ~ unquote_string }
link                  =  { "^" ~ unquote_string }

transaction_lines     =  { transaction_line+}
transaction_line      =  { indentation ~ (transaction_posting | key_value_line)? ~ space* ~ valuable_comment?  }
transaction_posting   =  { transaction_flag? ~ account_name ~ (space+ ~ posting_unit)? }

posting_unit   = { (posting_amount)? ~ posting_meta }
posting_amount = { number_expr ~ space* ~ commodity_name }
posting_meta   = { (space* ~ posting_cost_prefix ~ space* ~ posting_cost? ~ price_cost_date? ~ space* ~ posting_cost_postfix)? ~ space* ~ posting_price? }


posting_cost_prefix = { "{" }
posting_cost_postfix = { "}" }
posting_cost    =  { number_expr ~ space* ~ commodity_name }
price_cost_date = _{ space* ~ "," ~ space* ~ date }
posting_price   =  { posting_single_price | posting_total_price }

posting_single_price = { "@" ~ space* ~ number_expr ~ space* ~ commodity_name }
posting_total_price  = { "@@" ~ space* ~ number_expr ~ space* ~ commodity_name }

string_or_account = { account_name | string }

metas = { key_value_lines }

booking_method = { "\"" ~ ("STRICT" | "FIFO" | "LIFO" | "AVERAGE" | "AVERAGE_ONLY" | "NONE") ~ "\"" }

indentation = _{ line ~ space+ }

key_value_lines             = _{ (line ~ space+ ~ key_value_line_with_comment)+ }
key_value_line_with_comment =  _{ key_value_line ~ space* ~ comment? }
key_value_line              =  { string ~ space* ~ ":" ~ space* ~ string }

date      = { date_only }
date_only = { ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{1, 2} ~ "-" ~ ASCII_DIGIT{1, 2} }

time      = { time_part ~ ":" ~ time_part ~ ":" ~ time_part }
time_part = { ASCII_DIGIT{1, 2} }

account_name   =  { account_type ~ (":" ~ unquote_string)+ }
account_type   =  { "Assets" | "Liabilities" | "Equity" | "Income" | "Expenses" }
string         =  { unquote_string | quote_string }
unquote_string =  { ((!("\"" | ":" | "(" | ")" | "," | " " | "\t" | line) ~ ANY) | ASCII_ALPHANUMERIC | "." | "_" | "-")+ }
quote_string   = @{ "\"" ~ inner ~ "\"" }
commodity_name =  { ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "." | "_" | "-" | "'")* }

inner = @{ char* }
char  =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
  | "\\" ~ ("u" ~ "{" ~ ASCII_HEX_DIGIT{4} ~ "}")
}

space = _{ " " | "\t" }
line  = _{ NEWLINE }

number = @{
    (ASCII_DIGIT+ ~ (ASCII_DIGIT | "," | "_")*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

number_expr  =  { expr_atom ~ (space* ~ expr_bin_op ~ space* ~ expr_atom)* }
expr_atom    = _{ unary_minus? ~ space* ~ expr_primary }
expr_primary = _{ number | "(" ~ space* ~ number_expr ~ space* ~ ")" }
expr_bin_op  = _{ add | subtract | multiply | divide }
unary_minus  =  { "-" }
add          =  { "+" }
subtract     =  { "-" }
multiply     =  { "*" }
divide       =  { "/" }
